
&НаКлиенте
Процедура Загрузить(Команда)
	
	ЗагрузитьНаСервере(КаталогЗагрузки);
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(КаталогЗагрузки)
	
	ДокументХМЛ = Новый ЧтениеXML;
	// открываем файл
	ДокументХМЛ.ОткрытьФайл(КаталогЗагрузки+"\doc.xml");
	
	ОбъектДокумент = Неопределено;
	// считываем все элементы файла
	Пока ДокументХМЛ.Прочитать() Цикл
		// анализируем тип узла
		Если ДокументХМЛ.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			// считываем тип данных
			// ЛокальноеИмя - имя без лишних префиксов
			Тип8 = ИзXMLТипа(ДокументХМЛ.ЛокальноеИмя, ДокументХМЛ.URIПространстваИмен);
			// на основании данного типа будем создавать документ
			ИмяЭлемента = ДокументХМЛ.Имя;
			
			ИмяРеквизита = Неопределено;
			ИмяРеквизитаТЧ = Неопределено;
			
			Пока ДокументХМЛ.ПрочитатьАтрибут() Цикл
				// если это узел по загрузке документа
				// если документ загружается и уже идентифицировн - записываем документ
				Если ДокументХМЛ.Имя = "Загрузка" И ДокументХМЛ.Значение = "1" Тогда
					Если ОбъектДокумент<>Неопределено Тогда
						ОбъектДокумент.Записать();
						Сообщить("Записан документ "+ОбъектДокумент);
					КонецЕсли; 
					// найдем объект метаданных по типу в списке метаданных
					МетаДокумент = Метаданные.НайтиПоТипу(Тип8);
					// если такой тип документа существует в списке документов системы  - создаем документ нужного типа
					Если Метаданные.Документы.Содержит(МетаДокумент) Тогда
						ОбъектДокумент = Документы[МетаДокумент.Имя].СоздатьДокумент() ;
					Иначе
						Сообщить("Неожиданный объект");
						Возврат;
					КонецЕсли;
				ИначеЕсли ДокументХМЛ.Имя = "Имя" Тогда
					// определяем реквизиты шапки документа
					ИмяРеквизита = ДокументХМЛ.Значение;
				ИначеЕсли ДокументХМЛ.Имя = "ИмяТЧ" Тогда
					// определяем реквизиты табличной части документа
					ИмяРеквизитаТЧ = ДокументХМЛ.Значение;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ИмяЭлемента = "ТабличнаяЧасть" Тогда
				ИмяТЧ = ИмяРеквизита;
			ИначеЕсли ИмяЭлемента = "СтрокаТабличнойЧасти" Тогда
				СтрокаТЧ = ОбъектДокумент[ИмяТЧ].Добавить();
			ИначеЕсли ЗначениеЗаполнено(ИмяРеквизитаТЧ) Тогда
				// цикл по атрибутам табличной части
				Если ДокументХМЛ.Прочитать() Тогда
					СтрокаТЧ[ИмяРеквизитаТЧ] = XMLЗначение(Тип8, ДокументХМЛ.Значение);
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(ИмяРеквизита) Тогда
				// цикл по атрибутам шапки
				Если ДокументХМЛ.Прочитать() Тогда
					ОбъектДокумент[ИмяРеквизита] = XMLЗначение(Тип8, ДокументХМЛ.Значение);
				КонецЕсли;
				
			КонецЕсли; 
			
			
		КонецЕсли; 
		
	КонецЦикла; 
	
	Если ОбъектДокумент<>Неопределено Тогда
		//ОбъектДокумент.Номер = "";// если загрузка в ту же базу
		ОбъектДокумент.Записать();
		Сообщить("Записан документ "+ОбъектДокумент);
	КонецЕсли; 
	// закрываем файл
	ДокументХМЛ.Закрыть();

КонецПроцедуры // ЗагрузитьНаСервере()
 &НаКлиенте
Процедура КаталогЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Фильтр="Все файлы(*.*)|*.*";	
	ЗаголовокДиалога ="Выберите путь для отображения файлов";
	ТекКаталог = КаталогЗагрузки;
	КаталогЗагрузки=РаботаСФайламиКлиент.ВыборФайловИКаталогов("Каталог",Фильтр,ЗаголовокДиалога);
	
КонецПроцедуры
 