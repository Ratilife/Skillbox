Процедура СоздатьДокументРеализацияТоваров(Склад,ВидЦены,СразуПроводитьДокумент) Экспорт
	ТекДата = ТекущаяДата();
	// Создаем документ Реализация товаров
	РеализацияТоваров = Документы.РеализацияТоваров.СоздатьДокумент();
	РеализацияТоваров.Дата = ТекДата;
	РеализацияТоваров.Склад = Склад;
	// определение вида цены номенклатуры
	// определим остатки на складах
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНаСкладеОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(ЕСТЬNULL(ОстаткиНаСкладеОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток
		|ПОМЕСТИТЬ ОстатокНоменклатуры
		|ИЗ
		|	РегистрНакопления.ОстаткиНаСкладе.Остатки(
		|			&ТекДата,
		|			Склады = &Склад
		|				И Номенклатура.ВидНоменклатуры = &ВидНоменклатуры) КАК ОстаткиНаСкладеОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиНаСкладеОстатки.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстатокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ОстатокНоменклатуры.КоличествоОстаток КАК КоличествоОстаток,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена
		|ИЗ
		|	ОстатокНоменклатуры КАК ОстатокНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ТекДата, ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ОстатокНоменклатуры.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура";
	
	Запрос.УстановитьПараметр("ТекДата", ТекДата);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ВидНоменклатуры", Справочники.ВибыНоменклатура.Товар);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		 НоваяСтрока = РеализацияТоваров.Товары.Добавить();
		 НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		 НоваяСтрока.Количество = Выборка.КоличествоОстаток;
		 Если Выборка.Цена <> 0 Тогда
			 НоваяСтрока.ЦенаРеализации = Выборка.Цена;
			 НоваяСтрока.Стоимость = ОбщееНазначение.РасчитатьСуммуТабличнойЧасти(Выборка.КоличествоОстаток,Выборка.Цена);
		 КонецЕсли;
	КонецЦикла;
	Если СразуПроводитьДокумент Тогда
		РеализацияТоваров.Покупатель = Справочники.Контрагенты.ЭталонныйПокупатель;
		РеализацияТоваров.Записать(РежимЗаписиДокумента.Проведение);
	Иначе
		РеализацияТоваров.Записать();
	КонецЕсли;
КонецПроцедуры	