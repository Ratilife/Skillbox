
&НаКлиенте
Процедура Заполнить(Команда)
	// Вставить содержимое обработчика.
	
	Заполнять = Истина;
	Если Объект.Начисления.Количество()>0 ИЛИ Объект.Удержания.Количество()>0 ИЛИ Объект.ДополнительныеНачисления.Количество()>0 Тогда
		Если Вопрос("Очистить табличные части?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			Заполнять = Истина;
		Иначе
			Заполнять = Ложь;
		КонецЕсли;
	КонецЕсли; 
	
	Если Заполнять Тогда
		ЗаполнитьНаСервере();
	КонецЕсли; 
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()

	ЗаполнитьНачисления();
	ЗаполнитьДопНачисления();
	ЗаполнитьУдержания();

КонецПроцедуры // ЗаполнитьНаСервере()


&НаСервере
Процедура ЗаполнитьНачисления()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПлановыеНачисленияСрезПоследних.Сотрудник,
	               |	ПлановыеНачисленияСрезПоследних.Подразделение,
	               |	ПлановыеНачисленияСрезПоследних.ВидРасчета,
	               |	ПлановыеНачисленияСрезПоследних.Значение,
	               |	&НачалоПериода КАК Период
	               |ПОМЕСТИТЬ ТаблицаНачислений
	               |ИЗ
	               |	РегистрСведений.ПлановыеНачисления.СрезПоследних(&НачалоПериода, ) КАК ПлановыеНачисленияСрезПоследних
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПлановыеНачисления.Сотрудник,
	               |	ПлановыеНачисления.Подразделение,
	               |	ПлановыеНачисления.ВидРасчета,
	               |	ПлановыеНачисления.Значение,
	               |	ПлановыеНачисления.Период
	               |ИЗ
	               |	РегистрСведений.ПлановыеНачисления КАК ПлановыеНачисления
	               |ГДЕ
	               |	ПлановыеНачисления.Период МЕЖДУ &НачалоПериода И &КонецПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаНачислений.Сотрудник,
	               |	ТаблицаНачислений.Подразделение,
	               |	ТаблицаНачислений.ВидРасчета,
	               |	ТаблицаНачислений.Значение,
	               |	ТаблицаНачислений.Период,
	               |	МИНИМУМ(ЕСТЬNULL(ТаблицаНачислений1.Период, &КонецПериода)) КАК Период1
	               |ПОМЕСТИТЬ ТаблицаПериоды
	               |ИЗ
	               |	ТаблицаНачислений КАК ТаблицаНачислений
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНачислений КАК ТаблицаНачислений1
	               |		ПО ТаблицаНачислений.Сотрудник = ТаблицаНачислений1.Сотрудник
	               |			И ТаблицаНачислений.Подразделение = ТаблицаНачислений1.Подразделение
	               |			И ТаблицаНачислений.ВидРасчета = ТаблицаНачислений1.ВидРасчета
	               |			И ТаблицаНачислений.Период < ТаблицаНачислений1.Период
	               |ГДЕ
	               |	ТаблицаНачислений.Значение > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаНачислений.Сотрудник,
	               |	ТаблицаНачислений.Подразделение,
	               |	ТаблицаНачислений.ВидРасчета,
	               |	ТаблицаНачислений.Период,
	               |	ТаблицаНачислений.Значение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РаботникиОрганизацийСрезПоследних.Сотрудник,
	               |	РаботникиОрганизацийСрезПоследних.Подразделение,
	               |	РаботникиОрганизацийСрезПоследних.ТипГрафика,
	               |	&НачалоПериода КАК Период
	               |ПОМЕСТИТЬ РаботникиНаНачалоПериода
	               |ИЗ
	               |	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&НачалоПериода, ) КАК РаботникиОрганизацийСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период,
	               |	РаботникиОрганизаций.Сотрудник,
	               |	РаботникиОрганизаций.Подразделение,
	               |	РаботникиОрганизаций.ТипГрафика
	               |ПОМЕСТИТЬ РаботникиЗаПериод
	               |ИЗ
	               |	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	               |ГДЕ
	               |	РаботникиОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РаботникиОрганизаций.Сотрудник,
	               |	РаботникиОрганизаций.Подразделение,
	               |	РаботникиОрганизаций.ТипГрафика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РаботникиНаНачалоПериода.Сотрудник,
	               |	РаботникиНаНачалоПериода.Подразделение,
	               |	РаботникиНаНачалоПериода.ТипГрафика,
	               |	РаботникиНаНачалоПериода.Период
	               |ПОМЕСТИТЬ РаботникиНеСгруппированные
	               |ИЗ
	               |	РаботникиНаНачалоПериода КАК РаботникиНаНачалоПериода
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	РаботникиЗаПериод.Сотрудник,
	               |	РаботникиЗаПериод.Подразделение,
	               |	РаботникиЗаПериод.ТипГрафика,
	               |	РаботникиЗаПериод.Период
	               |ИЗ
	               |	РаботникиЗаПериод КАК РаботникиЗаПериод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РаботникиНеСгруппированные.Сотрудник,
	               |	РаботникиНеСгруппированные.Подразделение,
	               |	МАКСИМУМ(РаботникиНеСгруппированные.ТипГрафика) КАК ТипГрафика,
	               |	МАКСИМУМ(РаботникиНеСгруппированные.Период) КАК Период
	               |ПОМЕСТИТЬ РаботникиСгруппированные
	               |ИЗ
	               |	РаботникиНеСгруппированные КАК РаботникиНеСгруппированные
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РаботникиНеСгруппированные.Сотрудник,
	               |	РаботникиНеСгруппированные.Подразделение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПериоды.Сотрудник КАК Сотрудник,
	               |	ТаблицаПериоды.Подразделение КАК Подразделение,
	               |	ТаблицаПериоды.ВидРасчета,
	               |	ТаблицаПериоды.Значение КАК Размер,
	               |	ТаблицаПериоды.Период КАК ДатаНачала,
	               |	МАКСИМУМ(ТаблицаПериоды.Период1) КАК ДатаОкончания,
	               |	РаботникиСгруппированные.ТипГрафика
	               |ИЗ
	               |	ТаблицаПериоды КАК ТаблицаПериоды
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РаботникиСгруппированные КАК РаботникиСгруппированные
	               |		ПО ТаблицаПериоды.Сотрудник = РаботникиСгруппированные.Сотрудник
	               |			И ТаблицаПериоды.Подразделение = РаботникиСгруппированные.Подразделение
	               |			И ТаблицаПериоды.Период <= РаботникиСгруппированные.Период
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаПериоды.Сотрудник,
	               |	ТаблицаПериоды.Подразделение,
	               |	ТаблицаПериоды.ВидРасчета,
	               |	ТаблицаПериоды.Период,
	               |	ТаблицаПериоды.Значение,
	               |	РаботникиСгруппированные.ТипГрафика
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	ДатаНачала
	               |АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации));
	
	Объект.Начисления.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры // ЗаполнитьНачисления()

&НаСервере
Процедура ЗаполнитьУдержания()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПлановыеУдержанияСрезПоследних.Сотрудник,
	               |	ПлановыеУдержанияСрезПоследних.Подразделение,
	               |	ПлановыеУдержанияСрезПоследних.ВидРасчета,
	               |	ПлановыеУдержанияСрезПоследних.Значение КАК Размер,
	               |	&НачалоПериода КАК Период
	               |ПОМЕСТИТЬ Удержания
	               |ИЗ
	               |	РегистрСведений.ПлановыеУдержания.СрезПоследних(&НачалоПериода, ) КАК ПлановыеУдержанияСрезПоследних
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПлановыеУдержания.Сотрудник,
	               |	ПлановыеУдержания.Подразделение,
	               |	ПлановыеУдержания.ВидРасчета,
	               |	ПлановыеУдержания.Значение,
	               |	ПлановыеУдержания.Период
	               |ИЗ
	               |	РегистрСведений.ПлановыеУдержания КАК ПлановыеУдержания
	               |ГДЕ
	               |	ПлановыеУдержания.Период МЕЖДУ &НачалоПериода И &КонецПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	Удержания.Сотрудник КАК Сотрудник,
	               |	Удержания.Подразделение КАК Подразделение,
	               |	Удержания.ВидРасчета,
	               |	Удержания.Размер
	               |ИЗ
	               |	Удержания КАК Удержания
	               |ГДЕ
	               |	Удержания.Размер > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Удержания.Подразделение,
	               |	Удержания.ВидРасчета,
	               |	Удержания.Сотрудник,
	               |	Удержания.Размер
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	Подразделение
	               |АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации));
	
	Объект.Удержания.Загрузить(Запрос.Выполнить().Выгрузить());
	

КонецПроцедуры // ЗаполнитьНачисления()
// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ЗаполнитьДопНачисления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РаботникиОрганизацийСрезПоследних.Сотрудник,
		|	РаботникиОрганизацийСрезПоследних.Подразделение,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Премия) КАК ВидРасчета
		|ПОМЕСТИТЬ Работники
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&НачалоПериода, ) КАК РаботникиОрганизацийСрезПоследних
		|ГДЕ
		|	РаботникиОрганизацийСрезПоследних.Состояние = &Состояние
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РаботникиОрганизаций.Сотрудник,
		|	РаботникиОрганизаций.Подразделение,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ДополнительныеНачисления.Премия)
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
		|ГДЕ
		|	РаботникиОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаботникиОрганизаций.Состояние = &Состояние
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Работники.Сотрудник,
		|	Работники.Подразделение,
		|	Работники.ВидРасчета,
		|	&НачалоПериода КАК БазовыйПериодНачало,
		|	&КонецПериода КАК БазовыйПериодКонец
		|ИЗ
		|	Работники КАК Работники";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.ПериодРегистрации));
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияРаботников.Работает );
	Объект.ДополнительныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	

КонецПроцедуры // ЗаполнитьДопНачисления()
