
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// 1. Установка маркера Записи у регистра
	Движения.ОсновныеНачисленияСторнирование.Записывать=Истина;
	// 2. Запрос, в котором заполняется коллекция движений по регистру
	Запрос = Новый Запрос;
    Запрос.Текст =
	  "ВЫБРАТЬ
	 |	НачислениеЗаработнойПлатыСторнированиеНачисления.Ссылка.ПериодРегистрации 		   КАК ПериодРегистрации,
	 |	НачислениеЗаработнойПлатыСторнированиеНачисления.Сотрудник 						   КАК Сотрудник,
	 |	НачислениеЗаработнойПлатыСторнированиеНачисления.ВидРасчета 					   КАК ВидРасчета,
	 |	НачислениеЗаработнойПлатыСторнированиеНачисления.Размер 						   КАК Размер,
	 |	НачислениеЗаработнойПлатыСторнированиеНачисления.ДатаНачала 					   КАК ПериодДействияНачало,
	 |	КОНЕЦПЕРИОДА(НачислениеЗаработнойПлатыСторнированиеНачисления.ДатаОкончания, ДЕНЬ) КАК ПериодДействияКонец,
	 |	ВЫБОР
	 |		КОГДА НачислениеЗаработнойПлатыСторнированиеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновноеНачислениеСторнирование.Больничный)
	 |			ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗаработнойПлатыСторнированиеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
	 |		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	 |	КОНЕЦ 																			   КАК БазовыйПериодНачало,
	 |	ВЫБОР
	 |		КОГДА НачислениеЗаработнойПлатыСторнированиеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновноеНачислениеСторнирование.Больничный)
	 |			ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗаработнойПлатыСторнированиеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
	 |		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	 |	КОНЕЦ 																			   КАК БазовыйПериодКонец
	 |ИЗ
	 |	Документ.НачислениеЗаработнойПлатыСторнирование.Начисления КАК НачислениеЗаработнойПлатыСторнированиеНачисления
	 |ГДЕ
	 |	НачислениеЗаработнойПлатыСторнированиеНачисления.Ссылка = &Ссылка"  ;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	// 3. Заполнение коллекции движений по выборке результата запроса
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаДвижений = Движения.ОсновныеНачисленияСторнирование.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, Выборка);
	КонецЦикла;
	// 4. Получение дополнительных данных, позволяющих выполнить сторнирование записей
    //с прошлым периодом регистрации при вводе текущего набора записей
	Дополнение = Движения.ОсновныеНачисленияСторнирование.ПолучитьДополнение();
	Для каждого Запись Из Дополнение Цикл
		// 5. Заполнение записи сторно
		Движение = Движения.ОсновныеНачисленияСторнирование.Добавить();
		ЗаполнитьЗначенияСвойств(Движение,Запись);
        Движение.ПериодРегистрации = Запись.ПериодРегистрацииСторно;
        Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
        Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
        Движение.Сторно = Истина;
	КонецЦикла;
	// 6. Предварительная запись движений
	Движения.Записать(); 
    //7. Расчет записей регистра расчета
	РасчетЗП.РасчитатьСторнирование(Движения.ОсновныеНачисленияСторнирование,Ссылка);
КонецПроцедуры
